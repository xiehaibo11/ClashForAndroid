plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
}

android {
    compileSdk 33
    
    namespace 'com.github.kr328.clash.core'

    defaultConfig {
        minSdk 24
        targetSdk 33
        
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            assets.srcDirs += ["$buildDir/intermediates/dynamic_assets"]
            jniLibs.srcDirs += ["$buildDir/intermediates/native_output"]
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    
    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs += ["-opt-in=kotlin.RequiresOptIn"]
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.3"
    implementation "androidx.core:core-ktx:1.8.0"
}

afterEvaluate {
    def compileArch = ["arm64-v8a", "x86_64"]
    def options = new GolangBuildOptions(file("src/main/golang"), file("$buildDir/intermediates/native_output"), "23")

    def ts = compileArch.stream().map { abi ->
        tasks.register("golangBuildFor" + abi.replace("-", "").replace("_", ""), GolangBuildTask.class) {
            setOptions options, abi
        }
    }.toArray()

    for ( task in tasks ) {
        if ( task.name.contains("JniLib") )
            task.dependsOn(ts)
    }

    def ds = tasks.register("downloadMMDB", MMDBDowloadTask.class) {
        onlyIf {
            def targetFile = file("$buildDir/intermediates/cache/Country.mmdb")
            !targetFile.exists() || 
            targetFile.length() == 0 ||
            System.currentTimeMillis() - targetFile.lastModified() > 24 * 3600 * 1000L
        }

        output = "$buildDir/intermediates/cache/Country.mmdb"
    }

    def es = tasks.register("extractMMDB", Copy.class) {
        onlyIf {
            def sourceFile = file("$buildDir/intermediates/cache/Country.mmdb")
            def targetFile = file("$buildDir/intermediates/dynamic_assets/Country.mmdb")
            sourceFile.exists() && 
            sourceFile.length() > 0 &&
            sourceFile.lastModified() > targetFile.lastModified()
        }

        from("$buildDir/intermediates/cache") {
            include "Country.mmdb"
        }
        into "$buildDir/intermediates/dynamic_assets"

        dependsOn ds
    }

    for ( task in tasks ) {
        if ( task.name.contains("generate") && task.name.contains("Assets") )
            task.dependsOn(es)
    }
}

repositories {
    mavenCentral()
}